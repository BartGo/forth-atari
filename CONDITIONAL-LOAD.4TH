( BOILERPLATE CODE TO LOAD
( MAIN FORTH LIBRARIES WITHOUT
( LOADING ANYTHING MULTIPLE
( TIMES. WORKS WITH ATARI 8-BIT
( APX EXTENDED FIG FORTH.
( BY BARTGO, 2024, MIT LICENSE

( MARKER FOR "FORGET )
FORTH DEFINITIONS DECIMAL
: |APP| ;

( VECTORED "RUN" )
0 VARIABLE 'R ' CR 'R !
: RUN 'R CFA EXECUTE ;

( CONDITIONAL "LOAD" )
: ?LOAD ( f n -- ) 
  SWAP IF CR LOAD ELSE DROP THEN ; 

( STRING STORED IN PAD )
: $" ( -- ) ( $" XYZ" )
  ASCII " WORD HERE
  COUNT PAD OVER OVER C!
  1+ SWAP CMOVE ; IMMEDIATE

( IS THE WORD IN PAD UNKNOWN )
: ALIEN? ( -- f ) ( - in dict? )
  PAD CONTEXT @ @ (FIND)
  DUP 0= IF
    DROP PAD CURRENT @ @ (FIND)
  THEN 
  IF DROP DROP 0 ELSE 1 THEN ;
  
FORTH DEFINITIONS HEX

: TEXT HERE C/L 1+ BLANKS WORD
  HERE PAD C/L 1+ CMOVE ; 
: LINE DUP FFF0 AND 17 ?ERROR
  SCR @ (LINE) DROP ; 
: MARK 10 0 DO I LINE UPDATE
  DROP LOOP ;

VOCABULARY EDITOR IMMEDIATE
EDITOR DEFINITIONS HEX

: WHERE DUP B/SCR / DUP
  SCR ! ." SCR # " DECIMAL .
  SWAP C/L /MOD C/L * ROT BLOCK +
  CR C/L -TRAILING TYPE CR HERE
  C@ - SPACES 1 2FE C! 1C EMIT
  0 2FE C! [COMPILE] EDITOR QUIT ;
: -MOVE LINE C/L CMOVE UPDATE ;
: P 1 TEXT PAD 1+ SWAP -MOVE ; 
: E LINE C/L BLANKS UPDATE ;
: CLEAR  SCR ! 10 0 DO FORTH I
  EDITOR E LOOP ;

FORTH DEFINITIONS EDITOR DECIMAL

80 CLEAR
0 P ( INCLUDE MODULES IF NOT KNOWN )
1 P $" ASSEMBLER" ALIEN? 40 ?LOAD 
2 P $" ASSEMBLER" ALIEN? 40 ?LOAD
3 P $" GR."       ALIEN? 50 ?LOAD 
4 P $" GR."       ALIEN? 50 ?LOAD 
5 P $" FLOAT"     ALIEN? 60 ?LOAD 
6 P $" FLOAT"     ALIEN? 60 ?LOAD 
7 P RUN ;S

( WHEN RUN IS READY, REPLACE: )
(   ' NEW-RUN 'R ! )

FORTH DEFINITIONS 
80 LOAD 

;S
