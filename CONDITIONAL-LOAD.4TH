( BOILERPLATE CODE TO LOAD
( MAIN FORTH LIBRARIES WITHOUT
( LOADING ANYTHING MULTIPLE
( TIMES. WORKS WITH ATARI 8-BIT
( APX EXTENDED FIG FORTH.
( BY BARTGO, 2024, MIT LICENSE

( MARKER FOR "FORGET )
FORTH DEFINITIONS DECIMAL
: |APP| ;

( VIRTUAL SCREEN ZERO )
FORTH DEFINITIONS DECIMAL
  16368 CONSTANT LO ( START OF BUFFER AREA )
  17408 CONSTANT HI ( HI-LO / 1024 SCREENS )
: R/W
  >R B/BUF * LO + DUP HI > 6 ?ERROR
  R> IF SWAP ENDIF B/BUF CMOVE ;
' R/W CFA DUP
  ' BLOCK  48 + !
  ' BUFFER 44 + !
( Insert CFA of R/W into BLOCK and BUFFER  )
( and proceed as if testing disc. Only     )
( screen #0 is simulated, as HI-LO = 1024. )
( Originally, this changed R/W simulated   )
( screens 0 thru 9 in $4000 thru $6BFF,    )
( all with assumption that B/BUF is 128.   )

( VECTORED "RUN" )
0 VARIABLE 
: DUMMY-RUN BEGIN CR ." SYSTEM ERROR" AGAIN ;
'R ' DUMMY-RUN 'R !
: RUN 'R CFA EXECUTE ;

( CONDITIONAL "LOAD" )
: ?LOAD ( f n -- ) 
  SWAP IF CR LOAD ELSE DROP THEN ; 

( STRING STORED IN PAD )
: $" ( -- ) ( $" XYZ" )
  ASCII " WORD HERE
  COUNT PAD OVER OVER C!
  1+ SWAP CMOVE ; IMMEDIATE

( IS THE WORD IN PAD UNKNOWN? )
: ALIEN? ( -- f ) ( - in dict? )
  PAD CONTEXT @ @ (FIND)
  DUP 0= IF
    DROP PAD CURRENT @ @ (FIND)
  THEN 
  IF DROP DROP 0 ELSE 1 THEN ;
 
( FIG SCREEN EDITOR SUBSET )
FORTH DEFINITIONS HEX
: TEXT HERE C/L 1+ BLANKS WORD
  HERE PAD C/L 1+ CMOVE ; 
: LINE DUP FFF0 AND 17 ?ERROR
  SCR @ (LINE) DROP ; 
: MARK 10 0 DO I LINE UPDATE
  DROP LOOP ;
VOCABULARY EDITOR IMMEDIATE
EDITOR DEFINITIONS HEX
: WHERE DUP B/SCR / DUP
  SCR ! ." SCR # " DECIMAL .
  SWAP C/L /MOD C/L * ROT BLOCK +
  CR C/L -TRAILING TYPE CR HERE
  C@ - SPACES 1 2FE C! 1C EMIT
  0 2FE C! [COMPILE] EDITOR QUIT ;
: -MOVE LINE C/L CMOVE UPDATE ;
: P 1 TEXT PAD 1+ SWAP -MOVE ; 
: E LINE C/L BLANKS UPDATE ;
: CLEAR  SCR ! 10 0 DO FORTH I
  EDITOR E LOOP ;

( FILL IN THE VIRTUAL SCREEN WITH )
( INCLUDE STATEMENTS WHICH ALLOW  )
( CONDITIONAL SCREEN LOAD         )
FORTH DEFINITIONS EDITOR DECIMAL
0 CLEAR
0 P ( INCLUDE MODULES IF NOT KNOWN )
1 P $" ASSEMBLER" ALIEN? 40 ?LOAD 
2 P $" ASSEMBLER" ALIEN? 40 ?LOAD
3 P $" GR."       ALIEN? 50 ?LOAD 
4 P $" GR."       ALIEN? 50 ?LOAD 
5 P $" FLOAT"     ALIEN? 60 ?LOAD 
6 P $" FLOAT"     ALIEN? 60 ?LOAD 
7 P RUN ;S
FLUSH

( WHEN RUN IS READY, REPLACE: )
(   ' NEW-RUN 'R ! )

( INCLUDE PREREQUISITES AND RUN )
FORTH DEFINITIONS 
$" RUN" ALIEN? 0 ?LOAD 

;S
