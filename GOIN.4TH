EDITOR

19 SCR !
 0 P ( GOESINTO 1/2 )
 1 P ( Decompiler by R. D. Ackerman )
 2 P ( -- Forth Dimension Vol14 Nr2 )
 3 P : :GOIN ; 0 VARIABLE GIN 
 4 P : GIN+ CR GIN @ 2+ DUP GIN ! SPACES ;
 5 P : GCHK DUP @ 2+ ' COMPILE =
 6 P   IF  2+ DUP @ 2+ NFA ID. 2+
 7 P   ELSE DUP @ 2+ DUP ' LIT =
 8 P   OVER ' BRANCH  = OR OVER ' 0BRANCH = OR
 9 P   OVER ' (LOOP)  = OR SWAP ' (+LOOP) = OR
10 P   IF 2+ DUP @ SPACE . 2+ ELSE DUP @ 2+ ' CLIT =
11 P   IF 2+ DUP C@ SPACE . 1+ ELSE DUP @ 2+ 
12 P   ' (.") = IF 2+ DUP COUNT TYPE 
13 P   DUP C@ 1+ + ELSE 2+ THEN THEN THEN THEN 
14 P   -2 GIN +! ; 
15 P -->

20 SCR !
 0 P ( GOESINTO 2/2 ) 
 1 P : MYSELF LATEST PFA CFA , ; IMMEDIATE
 2 P : DIN CR GIN @ SPACES ;
 3 P : (GOESINTO) DUP CFA @ ' : CFA 
 4 P   @ = OVER ' ERROR = 0= AND
 5 P   IF BEGIN DUP @ DUP ' ;S CFA =
 6 P     OVER ' (;CODE) CFA = OR 0=
 7 P   WHILE 2+ DUP GIN+ NFA ID. KEY 
 8 P     DUP 81 = IF SP! QUIT ELSE 155 =
 9 P     IF MYSELF ELSE DROP THEN THEN
10 P     GCHK REPEAT 2+ DIN NFA ID.
11 P   THEN DROP ; ( 81=Q, 155=RETURN )
12 P : GOESINTO -FIND IF DROP 0 GIN !
13 P   (GOESINTO) ELSE ." NOT FOUND" CR THEN ;
14 P : GOIN GOESINTO ; : ;GOIN ;
15 P ;S ( "GOIN word", RET/SPC/Q ) 

17 CLEAR

FLUSH
